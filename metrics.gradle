group = "com.ice"
version = "1.1"
description = 'this is the project for study metrics created by ice'

//def runClass="com.ice.GaugeTest"
ext {
   metrics_version = "3.1.0"
   runClass="com.ice.GaugeTest" //com.ice.GetStarted
}

//apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
 	compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: "${metrics_version}"
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.3'
	compile 'com.h2database:h2:1.4.192'
    testCompile "junit:junit:4.12"
}

task run(type: JavaExec, dependsOn: 'classes') {
    classpath sourceSets.main.runtimeClasspath
    classpath configurations.runtime
    main = project.runClass
    //args = "The rain in Spain falls mainly in the plain".split().toList()
}

task showProjectProperties << {
   println "version:${version}"
   println name
   println path
   println project
   println buildDir

   println project.description
   println project.group
   println metrics_version
}

task init << {
    sourceSets*.java.srcDirs*.each { it.mkdirs() }
    sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}




//在build文件中自定义任务类型
task createDatabase(type: MySqlTask) {
    sql = 'CREATE DATABASE IF NOT EXISTS example'
}

task createUser(type: MySqlTask, dependsOn: createDatabase) {
    sql = "GRANT ALL PRIVILEGES ON example.* TO exampleuser@localhost IDENTIFIED BY 'passw0rd'"
}

task createTable(type: MySqlTask, dependsOn: createUser) {
    username = 'exampleuser'
    password = 'passw0rd'
    database = 'example'
    sql = 'CREATE TABLE IF NOT EXISTS users (id BIGINT PRIMARY KEY, username VARCHAR(100))'
}

class MySqlTask extends DefaultTask {
    //声明任务的属性
    def hostname = 'localhost'
    def port = 3306
    def sql
    def database
    def username = 'root'
    def password = 'password'

    //表示这是任务的动作
    @TaskAction
    def runQuery() {
        def cmd
        if(database) {
            cmd = "mysql -u ${username} -p${password} -h ${hostname} -P ${port} ${database} -e "
        }
        else {
            cmd = "mysql -u ${username} -p${password} -h ${hostname} -P ${port} -e "
        }
        project.exec {
            commandLine = cmd.split().toList() + sql
        }
    }
}
